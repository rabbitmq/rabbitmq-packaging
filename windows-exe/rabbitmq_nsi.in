Unicode false

; vim:ft=nsis:
; Use the "Modern" UI
!include MUI2.nsh
!include LogicLib.nsh
!define STRFUNC_USECALLARTIFICIALFUNCTION
!include StrFunc.nsh
!include WinMessages.nsh
!include FileFunc.nsh
!include WordFunc.nsh
!include x64.nsh

!addplugindir plugins

!define ERLANG_MIN_VERSION 26.0  ; https://www.rabbitmq.com/which-erlang.html#compatibility-matrix
!define ERLANG_MAX_VERSION

!define env_hklm 'HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"'
!define uninstall "Software\Microsoft\Windows\CurrentVersion\Uninstall\RabbitMQ"
!define MUI_COMPONENTSPAGE_SMALLDESC
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_TEXT "Start RabbitMQ service"
!define MUI_FINISHPAGE_RUN_FUNCTION FinishPage.Run
!define MUI_UNFINISHPAGE_NOAUTOCLOSE

;--------------------------------

; The name of the installer
Name "RabbitMQ Server %%VERSION%%"

; The file to write
OutFile "rabbitmq-server-%%VERSION%%.exe"

; Icons
!define MUI_ICON "rabbitmq.ico"

; The default installation directory is empty. The .onInit function
; below takes care of selecting the appropriate (32-bit vs. 64-bit)
; "Program Files".
InstallDir ""

; Registry key to check for directory (so if you install again, it will
; overwrite the old one automatically)
InstallDirRegKey HKLM "Software\VMware, Inc.\RabbitMQ Server" "Install_Dir"

; Request application privileges for Windows Vista
RequestExecutionLevel admin

SetCompressor /solid lzma

;--------------------------------

; Pages


;  !insertmacro MUI_PAGE_LICENSE "..\..\LICENSE-MPL-RabbitMQ"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY
  !insertmacro MUI_PAGE_INSTFILES
  !define MUI_PAGE_CUSTOMFUNCTION_SHOW FinishPage.Show
  !insertmacro MUI_PAGE_FINISH

  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  !define MUI_FINISHPAGE_TEXT "RabbitMQ Server %%VERSION%% has been uninstalled from your computer.$\n$\nPlease note that the log and database directories located at $APPDATA\RabbitMQ have not been removed. You can remove them manually if desired."
  !insertmacro MUI_UNPAGE_FINISH

;--------------------------------
;Languages

  !insertmacro MUI_LANGUAGE "English"

;--------------------------------

VIProductVersion "%%PRODUCT_VERSION%%"
VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductVersion" "%%VERSION%%"
VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductName" "RabbitMQ Server"
;VIAddVersionKey /LANG=${LANG_ENGLISH} "Comments" ""
VIAddVersionKey /LANG=${LANG_ENGLISH} "CompanyName" "VMware, Inc."
;VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalTrademarks" "" ; TODO ?
VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalCopyright" "Copyright (c) 2007-2022 VMware, Inc. or its affiliates.  All rights reserved."
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileDescription" "RabbitMQ Server"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileVersion" "%%VERSION%%"

var RABBITMQ_BASE
var RABBITMQ_LOG_BASE
var RABBITMQ_MNESIA_BASE
var RABBITMQ_PLUGINS_DIR

var FinishPage_Run_Enabled

; The stuff to install
Section "RabbitMQ Server (required)" Rabbit

  SectionIn RO

  ; Set output path to the installation directory.
  SetOutPath $INSTDIR

  ; Put files there
  File /r /x "plugins" "rabbitmq_server-%%VERSION%%"
  File "rabbitmq.ico"

  ReadEnvStr $RABBITMQ_PLUGINS_DIR "RABBITMQ_PLUGINS_DIR"
  ${If} ${Errors}
    StrCpy $RABBITMQ_PLUGINS_DIR "$INSTDIR\rabbitmq_server-%%VERSION%%\plugins"
  ${EndIf}

  ; Set output path to the RABBITMQ_PLUGINS_DIR
  SetOutPath $RABBITMQ_PLUGINS_DIR

  ; Put plugins there
  File /r "rabbitmq_server-%%VERSION%%\plugins\*"

  ; Set output path to the user's data directory
  SetOutPath $RABBITMQ_BASE

  IfFileExists advanced.config 0 +2
    Goto config_written
  IfFileExists rabbitmq.config 0 +3
    Rename rabbitmq.config advanced.config
    Goto config_written
  ClearErrors
  FileOpen $0 advanced.config w
  IfErrors config_written
  FileWrite $0 "[]."
  FileClose $0
  config_written:

  ; Write the installation path into the registry
  WriteRegStr HKLM "SOFTWARE\VMware, Inc.\RabbitMQ Server" "Install_Dir" "$INSTDIR"

  ; Write the uninstall keys for Windows
  WriteRegStr HKLM ${uninstall} "DisplayName" "RabbitMQ Server %%VERSION%%"
  WriteRegStr HKLM ${uninstall} "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
  WriteRegStr HKLM ${uninstall} "DisplayIcon" "$INSTDIR\rabbitmq.ico"
  WriteRegStr HKLM ${uninstall} "Publisher" "VMware, Inc."
  WriteRegStr HKLM ${uninstall} "DisplayVersion" "%%VERSION%%"
  WriteRegDWORD HKLM ${uninstall} "NoModify" 1
  WriteRegDWORD HKLM ${uninstall} "NoRepair" 1

  ${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
  IntFmt $0 "0x%08X" $0
  WriteRegDWORD HKLM "${uninstall}" "EstimatedSize" "$0"

  WriteUninstaller "uninstall.exe"

  ; Set permissions to ensure a non-privileged user cannot write to the install
  ; directory:
  ;     1. Disable permissions inheritance from the parent directory (which may
  ;        give all permissions to non-privileged users).
  ;     2. Give ReadAndExecute and Synchronize permissions only.
  ;
  ; We do this at the end in case we mess permissions up: if we did this
  ; earlier and removed permissions to the setup process, it wouldn't be able
  ; to write the uninstall.exe file.

  AccessControl::DisableFileInheritance "$INSTDIR"
  Pop $R0
  ${If} $R0 == error
    Pop $R0
    DetailPrint `Failed to disable permissions inheritance on $INSTDIR: $R0`
  ${EndIf}

  AccessControl::SetOnFile "$INSTDIR" "(BU)" "GenericRead + GenericExecute"
  Pop $R0
  ${If} $R0 == error
    Pop $R0
    DetailPrint `Failed to set user permissions on $INSTDIR: $R0`
  ${EndIf}
SectionEnd

;--------------------------------

Section "RabbitMQ Service" RabbitService
  DetailPrint "Installing RabbitMQ service..."
  ExecDos::exec /DETAILED '"$INSTDIR\rabbitmq_server-%%VERSION%%\sbin\rabbitmq-service.bat" install' ""
  Pop $0 # return value
  ${If} $0 <> 0
    MessageBox MB_OK "rabbitmq-service.bat install exited with code $0."
    Abort
  ${EndIf}
  ReadEnvStr $1 "HOMEDRIVE"
  ReadEnvStr $2 "HOMEPATH"
  Delete "$1$2\.erlang.cookie"
  ${If} ${RunningX64}
    ${DisableX64FSRedirection}
  ${EndIf}
  IfFileExists "$SYSDIR\config\systemprofile\.erlang.cookie" 0 cookie_check_windir
    CopyFiles "$SYSDIR\config\systemprofile\.erlang.cookie" "$1$2\.erlang.cookie"
    Goto cookie_done
  cookie_check_windir:
  IfFileExists "$WINDIR\.erlang.cookie" 0 cookie_done
    CopyFiles "$WINDIR\.erlang.cookie" "$1$2\.erlang.cookie"
  cookie_done:
  ${If} ${RunningX64}
    ${EnableX64FSRedirection}
  ${EndIf}
SectionEnd

;--------------------------------

Section "Start Menu Shortcuts" RabbitStartMenu
  ; In case the service is not installed, or the service installation fails,
  ; make sure these exist or Explorer will get confused.
  CreateDirectory "$RABBITMQ_LOG_BASE"
  CreateDirectory "$RABBITMQ_MNESIA_BASE"

  CreateDirectory "$SMPROGRAMS\RabbitMQ Server"
  CreateShortCut "$SMPROGRAMS\RabbitMQ Server\Uninstall RabbitMQ.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Plugins.lnk" "$RABBITMQ_PLUGINS_DIR"
  CreateShortCut "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Logs.lnk" "$RABBITMQ_LOG_BASE"
  CreateShortCut "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Database Directory.lnk" "$RABBITMQ_MNESIA_BASE"

  CreateShortCut "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Service - (re)install.lnk" "%comspec%" '/k "$INSTDIR\rabbitmq_server-%%VERSION%%\sbin\rabbitmq-service.bat" install & if not errorlevel 1 exit /b 0' "$INSTDIR\rabbitmq.ico"
  ShellLink::SetRunAsAdministrator "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Service - (re)install.lnk"

  CreateShortCut "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Service - remove.lnk" "%comspec%" '/k "$INSTDIR\rabbitmq_server-%%VERSION%%\sbin\rabbitmq-service.bat" remove & if not errorlevel 1 exit /b 0' "$INSTDIR\rabbitmq.ico"
  ShellLink::SetRunAsAdministrator "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Service - remove.lnk"

  CreateShortCut "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Service - start.lnk" "%comspec%" '/k "$INSTDIR\rabbitmq_server-%%VERSION%%\sbin\rabbitmq-service.bat" start & if not errorlevel 1 exit /b 0' "$INSTDIR\rabbitmq.ico"
  ShellLink::SetRunAsAdministrator "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Service - start.lnk"

  CreateShortCut "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Service - stop.lnk" "%comspec%" '/k "$INSTDIR\rabbitmq_server-%%VERSION%%\sbin\rabbitmq-service.bat" stop & if not errorlevel 1 exit /b 0' "$INSTDIR\rabbitmq.ico"
  ShellLink::SetRunAsAdministrator "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Service - stop.lnk"

  SetOutPath "$INSTDIR\rabbitmq_server-%%VERSION%%\sbin"
  CreateShortCut "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Command Prompt (sbin dir).lnk" "$SYSDIR\cmd.exe" "/k cd /d $INSTDIR\rabbitmq_server-%%VERSION%%\sbin" "$SYSDIR\cmd.exe"
  ShellLink::SetRunAsAdministrator "$SMPROGRAMS\RabbitMQ Server\RabbitMQ Command Prompt (sbin dir).lnk"

  SetOutPath $INSTDIR
SectionEnd

;--------------------------------

; Section descriptions

LangString DESC_Rabbit ${LANG_ENGLISH} "The RabbitMQ Server."
LangString DESC_RabbitService ${LANG_ENGLISH} "Set up RabbitMQ as a Windows Service."
LangString DESC_RabbitStartMenu ${LANG_ENGLISH} "Add some useful links to the start menu."

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${Rabbit} $(DESC_Rabbit)
  !insertmacro MUI_DESCRIPTION_TEXT ${RabbitService} $(DESC_RabbitService)
  !insertmacro MUI_DESCRIPTION_TEXT ${RabbitStartMenu} $(DESC_RabbitStartMenu)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------

; Uninstaller

Section "Uninstall"

  ; Remove registry keys
  DeleteRegKey HKLM ${uninstall}
  DeleteRegKey HKLM "SOFTWARE\VMware, Inc.\RabbitMQ Server"

  ; TODO these will fail if the service is not installed - do we care?
  DetailPrint "Stopping RabbitMQ service..."
  ExecDos::exec /DETAILED '"$INSTDIR\rabbitmq_server-%%VERSION%%\sbin\rabbitmq-service.bat" stop' ""
  DetailPrint "Removing RabbitMQ service..."
  ExecDos::exec /DETAILED '"$INSTDIR\rabbitmq_server-%%VERSION%%\sbin\rabbitmq-service.bat" remove' ""

  ; Remove files and uninstaller
  RMDir /r "$INSTDIR\rabbitmq_server-%%VERSION%%"
  Delete "$INSTDIR\rabbitmq.ico"
  Delete "$INSTDIR\uninstall.exe"
  RMDir "$INSTDIR"

  ; Remove start menu items
  RMDir /r "$SMPROGRAMS\RabbitMQ Server"

  DeleteRegValue ${env_hklm} ERLANG_HOME
  SendMessage ${HWND_BROADCAST} ${WM_WININICHANGE} 0 "STR:Environment" /TIMEOUT=5000

SectionEnd

;--------------------------------

; Functions

Function .onInit
  ; By default, always install in "\Program Files", not matter if we run
  ; on a 32-bit or 64-bit Windows.
  ${If} $INSTDIR == "";
    ${If} ${RunningX64}
      StrCpy $INSTDIR "$PROGRAMFILES64\RabbitMQ Server"
    ${Else}
      StrCpy $INSTDIR "$PROGRAMFILES\RabbitMQ Server"
    ${EndIf}
  ${EndIf}

  ReadEnvStr $RABBITMQ_BASE "RABBITMQ_BASE"
  ${If} ${Errors}
    StrCpy $RABBITMQ_BASE "$APPDATA\RabbitMQ"
  ${EndIf}

  ReadEnvStr $RABBITMQ_LOG_BASE "RABBITMQ_LOG_BASE"
  ${If} ${Errors}
    StrCpy $RABBITMQ_LOG_BASE "$RABBITMQ_BASE\log"
  ${EndIf}

  ReadEnvStr $RABBITMQ_MNESIA_BASE "RABBITMQ_MNESIA_BASE"
  ${If} ${Errors}
    StrCpy $RABBITMQ_MNESIA_BASE "$RABBITMQ_BASE\db"
  ${EndIf}

  Call findErlang

  ReadRegStr $0 HKLM ${uninstall} "UninstallString"
  ${If} $0 != ""
    MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION "RabbitMQ is already installed. $\n$\nClick 'OK' to remove the previous version or 'Cancel' to cancel this installation." /SD IDOK IDOK rununinstall IDCANCEL norun

    norun:
    Abort

    rununinstall:
    ;Run the uninstaller
    ClearErrors
    ExecWait '"$INSTDIR\uninstall.exe" /S _?=$INSTDIR'
    Delete "$INSTDIR\uninstall.exe"
    RMDir "$INSTDIR"
    ; the unistaller removes the ERLANG_HOME.
    ; called again since this is an update
    Call findErlang

  ${EndIf}

  ; disable RabbitService section if command line has /NOSERVICEINSTALL flag
  ${GetParameters} $7
  ClearErrors
  ${GetOptions} $7 /NOSERVICEINSTALL $0
  ${IfNot} ${Errors}
    SectionGetFlags ${RabbitService} $0
    IntOp $0 $0 | ${SF_SELECTED}
    IntOp $0 $0 ^ ${SF_SELECTED}
    SectionSetFlags ${RabbitService} $0
  ${EndIf}

  ; disable RabbitStartMenu section if command line has /NOSTARTMENU flag
  ${GetParameters} $7
  ClearErrors
  ${GetOptions} $7 /NOSTARTMENU $0
  ${IfNot} ${Errors}
    SectionGetFlags ${RabbitStartMenu} $0
    IntOp $0 $0 | ${SF_SELECTED}
    IntOp $0 $0 ^ ${SF_SELECTED}
    SectionSetFlags ${RabbitStartMenu} $0
  ${EndIf}
FunctionEnd

Function .onInstSuccess
  ${If} ${Silent}
    Call FinishPage.Show
    ${If} $FinishPage_Run_Enabled <> 0
      Call FinishPage.Run
    ${EndIf}
  ${EndIf}
FunctionEnd

; Trim
;   Removes leading & trailing whitespace from a string
; Usage:
;   Push
;   Call Trim
;   Pop
Function Trim
  Exch $R1 ; Original string
  Push $R2

  Loop:
  StrCpy $R2 "$R1" 1
  StrCmp "$R2" " " TrimLeft
  StrCmp "$R2" "$\r" TrimLeft
  StrCmp "$R2" "$\n" TrimLeft
  StrCmp "$R2" "$\t" TrimLeft
  GoTo Loop2

  TrimLeft:
  StrCpy $R1 "$R1" "" 1
  Goto Loop

  Loop2:
  StrCpy $R2 "$R1" 1 -1
  StrCmp "$R2" " " TrimRight
  StrCmp "$R2" "$\r" TrimRight
  StrCmp "$R2" "$\n" TrimRight
  StrCmp "$R2" "$\t" TrimRight
  GoTo Done

  TrimRight:
  StrCpy $R1 "$R1" -1
  Goto Loop2

  Done:
  Pop $R2
  Exch $R1
FunctionEnd

Function findErlang

  StrCpy $0 0
  StrCpy $2 "not-found"
  ${Do}
    EnumRegKey $1 HKLM Software\Ericsson\Erlang $0
    ${If} $1 = ""
      ${Break}
    ${EndIf}
    ${If} $1 <> "ErlSrv"
      StrCpy $2 $1
    ${EndIf}

    IntOp $0 $0 + 1
  ${Loop}

  ${If} $2 = "not-found"
    MessageBox MB_YESNO|MB_ICONEXCLAMATION "Erlang could not be detected.$\nYou must install Erlang before installing RabbitMQ. Would you like the installer to open a browser window to the Erlang download site?" IDNO abort
    ExecShell "open" "https://www.erlang.org/downloads"
    abort:
    Abort
  ${Else}
    Var /GLOBAL erlang_otp_dir
    Var /GLOBAL erlang_otp_version_file
    Var /GLOBAL erlang_otp_version

    ReadRegStr $erlang_otp_dir HKLM "Software\Ericsson\Erlang\$2" ""
    DetailPrint "Erlang/OTP install found in $erlang_otp_dir"

    FindFirst $0 $1 $erlang_otp_dir\releases\*.*
    loop:
      StrCmp $1 "" done
      StrCpy $erlang_otp_version $1
      FindNext $0 $1
      Goto loop
    done:
    FindClose $0

    StrCpy $erlang_otp_version_file $erlang_otp_dir\releases\$erlang_otp_version\OTP_VERSION
    DetailPrint "Looking for Erlang/OTP version in file $erlang_otp_version_file"

    ClearErrors
    FileOpen $1 $erlang_otp_version_file r
    IfErrors otp_version_error compare_otp_versions

    otp_version_error:
    MessageBox MB_OK|MB_ICONEXCLAMATION "Failed to read the version of Erlang/OTP from file $erlang_otp_version_file"
    Abort

    compare_otp_versions:
    FileRead $1 $erlang_otp_version
    FileClose $1
    Push $erlang_otp_version
    Call Trim
    Pop $erlang_otp_version
    DetailPrint "Found Erlang/OTP version $erlang_otp_version"

    ${VersionCompare} $erlang_otp_version ${ERLANG_MIN_VERSION} $3
    ${If} $3 = 2
      MessageBox MB_OK|MB_ICONEXCLAMATION "Your installed version of Erlang ($erlang_otp_version) is too old. Please install a more recent version [${ERLANG_MIN_VERSION},${ERLANG_MAX_VERSION})."
      Abort
    ${EndIf}

    ${If} "${ERLANG_MAX_VERSION}" != ""
      ${VersionCompare} $erlang_otp_version "${ERLANG_MAX_VERSION}" $3
      ${If} $3 <> 2
        MessageBox MB_OK|MB_ICONEXCLAMATION "Your installed version of Erlang ($erlang_otp_version) is too new. Please install a less recent version [${ERLANG_MIN_VERSION},${ERLANG_MAX_VERSION})."
        Abort
      ${EndIf}
    ${EndIf}

    ; See https://nsis.sourceforge.io/Setting_Environment_Variables
    WriteRegExpandStr ${env_hklm} ERLANG_HOME $erlang_otp_dir
    SendMessage ${HWND_BROADCAST} ${WM_WININICHANGE} 0 "STR:Environment" /TIMEOUT=5000

    ; On Windows XP changing the permanent environment does not change *our*
    ; environment, so do that as well.
    System::Call 'Kernel32::SetEnvironmentVariableA(t, t) i("ERLANG_HOME", "$erlang_otp_dir").r0'
  ${EndIf}

FunctionEnd

Function FinishPage.Run
  ExecDos::exec '"$INSTDIR\rabbitmq_server-%%VERSION%%\sbin\rabbitmq-service.bat" start' ""
  Pop $0 # return value
  ${If} $0 <> 0
    MessageBox MB_OK "rabbitmq-service.bat start exited with code $0."
    Abort
  ${EndIf}
FunctionEnd

Function FinishPage.Show
  StrCpy $FinishPage_Run_Enabled 1
  SectionGetFlags ${RabbitService} $R0
  IntOp $R0 $R0 & ${SF_SELECTED}
  ${If} $R0 <> ${SF_SELECTED}
    Goto hide
  ${EndIf}
  ${GetParameters} $7
  ClearErrors
  ${GetOptions} $7 /NOSERVICERUN $0
  ${IfNot} ${Errors}
    Goto disable
  ${EndIf}
  Return

hide:
  ShowWindow $mui.FinishPage.Run 0
disable:
  StrCpy $FinishPage_Run_Enabled 0
  SendMessage $mui.FinishPage.Run ${BM_SETCHECK} ${BST_UNCHECKED} 0
FunctionEnd
